apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'

// Production keystore
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


ext.versionMajor = 1
ext.versionMinor = 0
ext.versionPatch = 1
ext.versionClassifier = null
ext.isSnapshot = true  //only true while in development
ext.minimumSdkVersion = 21

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.app.cheffyuser"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode generateVersionCode() // 210100001
        versionName generateVersionName() // 1.0.0
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
        resConfigs "en"
        multiDexEnabled true
    }

    signingConfigs {


        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        debug {
            debuggable true

            versionNameSuffix ".debug"
            resValue("bool", "FIREBASE_CRASH_ENABLED", "false")
            //signingConfig signingConfigs.debug          //GET APP signingConfig for debug
        }

        release {
            resValue("bool", "FIREBASE_CRASH_ENABLED", "true")
            signingConfig signingConfigs.release          //GET APP signingConfig
            debuggable true
            //shrinkResources true
            //minifyEnabled true
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
        exclude 'META-INF/LICENSE'
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    dexOptions {
        //may speed up incremental builds
        preDexLibraries true
    }

    //to enable kt parcelables
    androidExtensions {
        experimental = true
    }

}

dependencies {
    def okhttp3_version = "3.14.1"
    def retrofit2_version = "2.6.0"
    def arch_version = '2.2.0-alpha01'
    def kotlinCoroutineVersion = "1.0.1"


    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.android.support:multidex:1.0.3'

    //play services
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.gms:play-services-places:17.0.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'

    //design libs
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.2.0-alpha02'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'

    //firebase
    implementation 'com.google.firebase:firebase-core:17.2.1'
    implementation 'com.google.firebase:firebase-messaging:20.0.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'


    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'


    //https://github.com/Karumi/Dexter
    implementation 'com.karumi:dexter:5.0.0'

    //https://github.com/hdodenhof/CircleImageView
    implementation 'de.hdodenhof:circleimageview:3.0.0'

    //material stepper
    implementation 'com.stepstone.stepper:material-stepper:4.3.1'

    //https://github.com/blackfizz/EazeGraph
    implementation 'com.github.blackfizz:eazegraph:1.2.2@aar'
    implementation 'com.nineoldandroids:library:2.4.0'

    //material ripple
    implementation 'com.balysv:material-ripple:1.0.2'

    implementation 'com.github.jd-alexander:LikeButton:0.2.3'

    //retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit2_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2_version"

    //Okhttp3
    implementation "com.squareup.okhttp3:okhttp:$okhttp3_version"

    //http logger
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"

    //glide libs
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'com.github.bumptech.glide:annotations:4.9.0'
    implementation('com.github.bumptech.glide:okhttp3-integration:4.0.0') {
        exclude group: 'glide-parent'
    }
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'

    // Circular image
    implementation 'com.mikhaellopez:circularimageview:3.2.0'

    //shimmer effect
    implementation 'com.facebook.shimmer:shimmer:0.4.0@aar'

    //timber for logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //droidnet internet
    implementation 'com.job:droidnet:2.0.0'

    //droid location
    implementation 'com.github.JobGetabu:DroidLocation:v3.0.0'

    //lottie
    implementation 'com.airbnb.android:lottie:2.8.0'

    //loader
    implementation 'com.github.mayuce:LottieAlertDialog:1.1'

    //shape of view
    implementation 'com.github.florent37:shapeofview:1.4.7'

    //material rating-bar
    implementation 'me.zhanghai.android.materialratingbar:library:1.3.2'

    //like button
    implementation 'com.github.varunest:sparkbutton:1.0.6'

    //image pix
    implementation 'com.fxn769:pix:1.4.4'

    //ucrop
    implementation 'com.github.yalantis:ucrop:2.2.2'

    //search view
    implementation 'br.com.mauker.materialsearchview:materialsearchview:1.2.3'

    //runtime permissions
    implementation 'com.karumi:dexter:5.0.0'

    // Facebook sdk
    implementation 'com.facebook.android:facebook-login:5.13.0'

    // Google login sdk
    implementation 'com.google.android.gms:play-services-auth:17.0.0'

    //places sdk
    implementation 'com.google.android.libraries.places:places:1.1.0'

    //play update core
    implementation 'com.google.android.play:core:1.6.4'

    //stripe
    implementation 'com.stripe:stripe-android:9.0.1'

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$arch_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$arch_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$arch_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$arch_version"

    //Kotlin Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutineVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutineVersion"

}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null && ext.isSnapshot) {
        ext.versionClassifier = "SNAPSHOT"
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}
